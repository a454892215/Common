apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.test.util"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        flavorDimensions "1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }


    signingConfigs {
        signing {
            keyAlias 'test'
            keyPassword '123456'
            storeFile file('../keystore.jks')
            storePassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_DEBUG', 'false'
            signingConfig signingConfigs.signing
            debuggable false
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_DEBUG', 'true'
            signingConfig signingConfigs.signing
            debuggable true
        }
    }

    productFlavors {

        product_1 {
            /* 完成 no */
            applicationId "com.test.product_1"
            versionCode 101
            versionName "1.0.1"
            resValue "string", "app_name", "测试1"
            resValue "bool", "auto_updates", 'true'
        }

        product_2 {
            /* 完成 no */
            applicationId "com.test.product_2"
            versionCode 101
            versionName "1.0.1"
            resValue "string", "app_name", "测试2"
            resValue "bool", "auto_updates", 'true'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def appInfo = getAppInfo() + variant.name
            buildConfigField('String', 'app_info', "\"" + appInfo + "\"")
            outputFileName = appInfo + ".apk"
        }
    }
}

def getAppInfo() {
    return "V" + android.defaultConfig.versionName + "(" + android.defaultConfig.versionCode + ")" + getLastCommitInfo() + new Date().format('yy年MM月dd日HH时mm分ss秒')
}

static def getLastCommitInfo() {
    def p1 = 'git log -1 --pretty=format:"%h-%ci"'.execute()
    String logInfo = getGitInfo(p1)
    def p2 = 'git symbolic-ref --short HEAD'.execute()
    String currentBranch = getGitInfo(p2).trim()
    String info = "[git-" + currentBranch + "-" + logInfo + "]-"
    return info
}

static String getGitInfo(p1) {
    p1.waitFor()
    InputStream is = p1.getInputStream()
    String text = is.getText("utf-8").replace(":", "_").replace("+0800", "")
    is.close()
    text
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':common')
    //provided 有错就替换为 compileOnly
    compileOnly 'de.robv.android.xposed:api:82'
    //如果需要引入文档，方便查看的话
    compileOnly 'de.robv.android.xposed:api:82:sources'
}
